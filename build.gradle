plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
}

compileJava {
  sourceCompatibility = 1.7
  targetCompatibility = 1.7
}

version = '1.0'

mainClassName = 'nl.esciencecenter.newsreader.hbase.Main'

repositories {
    mavenCentral()
    jcenter()
    maven{ url 'http://conjars.org/repo/' }
}

ext.cascadingVersion = '2.6.3'
ext.hadoopVersion = '2.6.0'
ext.hbaseVersion = '0.98.4-hadoop2'

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.mockito:mockito-core:1.+'
    testCompile 'joda-time:joda-time:2.7'
    compile "org.apache.hadoop:hadoop-common:${hadoopVersion}"
    compile "org.apache.hadoop:hadoop-hdfs:${hadoopVersion}"
    compile "org.apache.hbase:hbase-client:${hbaseVersion}"
    compile "org.apache.hbase:hbase-server:${hbaseVersion}"
    // used for cli arguments parsing
    compile 'com.beust:jcommander:1.48'
    // used to read bz2 files
    compile 'org.apache.commons:commons-compress:1.9'
    compile "cascading:cascading-core:${cascadingVersion}"
    compile "cascading:cascading-local:${cascadingVersion}"
    compile "cascading:cascading-hadoop2-mr1:${cascadingVersion}"
    compile "cascading-hbase:cascading-hbase-hadoop2-mr1:3.0.0-wip-12"
}

// Allow args from `gradle run` to be send to main class
// For example: gradle run -Pargs='dump ....'
run {
    if(project.hasProperty('args')){
        args project.args.split('\\s+')
    }
}

shadowJar {
    dependencies {
        // shadowJar does not exclude the dependencies of the dependencies (transitive)
        // and hbase jars must be passed with job
        // so making shadow jar small is not possible without flattening the dependency tree
        exclude(dependency("org.apache.hadoop:hadoop-common:${hadoopVersion}"))
        exclude(dependency("org.apache.hadoop:hadoop-hdfs:${hadoopVersion}"))
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
